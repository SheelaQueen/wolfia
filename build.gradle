plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.gorylenko.gradle-git-properties' version '1.4.21' //provide meta info about the git repo
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id 'com.jfrog.bintray' version '1.8.0'
}

ext {
    moduleName = 'wolfia'
    buildNumber = (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV')
}
group = 'space.npstr.wolfia'
//noinspection GroovyAssignabilityCheck
version = '0.13.4-' + project.ext.buildNumber


mainClassName = 'space.npstr.wolfia.Wolfia'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    jcenter()                               // JDA, maybe others
    mavenCentral()                          // everything else
    maven { url 'https://dl.bintray.com/napster/SqlSauce' }
    mavenLocal()                            // local maven repo, mostly for testing stuff
    maven { url 'https://jitpack.io' }      //for getting builds from github
}

dependencies {
    //@formatter:off
    ext.jdaVersion                = '3.6.0_359'
    ext.logbackVersion            = '1.2.3'
    ext.sentryVersion             = '1.7.3'
    ext.discordAppenderVersion    = '1.0.0'
    ext.yamlVersion               = '1.21'
    ext.okhttpVersion             = '3.10.0'
    ext.sqlsauceVersion           = '0.0.14'
    ext.jaxbApiVersion            = '2.3.0'
    ext.caffeineVersion           = '2.6.2'
    ext.dsProxyVersion            = '1.4.8'

    compile group: 'net.dv8tion',    name: 'JDA',                      version: ext.jdaVersion             //discord API
    compile group: 'ch.qos.logback', name: 'logback-classic',          version: ext.logbackVersion         //logging framework
    compile group: 'space.npstr',    name: 'logback-discord-appender', version: ext.discordAppenderVersion //log to discord
    compile group: 'io.sentry',      name: 'sentry-logback',           version: ext.sentryVersion          //error aggregation
    compile group: 'org.yaml',       name: 'snakeyaml',                version: ext.yamlVersion            //parse yaml files
    compile group: 'com.squareup.okhttp3', name: 'okhttp',             version: ext.okhttpVersion          //http client
    compile group: 'space.npstr.SqlSauce', name: 'sqlsauce-core',      version: ext.sqlsauceVersion        //db stack
    compile group: 'space.npstr.SqlSauce', name: 'discord-entities',   version: ext.sqlsauceVersion        //moar db entities
    compile group: 'javax.xml.bind', name: 'jaxb-api',                 version: ext.jaxbApiVersion         //required by hibernate for java 9
    compile group: 'com.github.ben-manes.caffeine', name:'caffeine',   version: ext.caffeineVersion        // caches
    compile group: 'net.ttddyy',     name: 'datasource-proxy',         version: ext.dsProxyVersion         // log proxy for db queries

    //@formatter:on
}


import org.apache.tools.ant.filters.ReplaceTokens

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}

processResources {
    //inject values into app.properties
    filesMatching('**/app.properties') {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName,
                "env.BUILD_NUMBER"  : project.ext.buildNumber,
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}

compileJava.dependsOn 'clean'
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

shadowJar {
    archiveName = 'wolfia.jar'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
        from 'build/libs'
        into '.'
        rename '(.+)\\.(.+)', '$1-' + project.version + '.$2'
    }
    dryRun = false
    publish = true
    pkg {
        repo = 'wolfia'
        name = 'beta'
        userOrg = user
        version {
            name = project.version
        }
    }
}
